/-
Copyright (c) 2024 Quang Dao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Quang Dao, Fawad Haider
-/

module

public import VCVio.OracleComp.QueryTracking.RandomOracle.Basic

/-!
  # Merkle Trees as a vector commitment

  This file defines the vector-style Merkle tree API: the oracle `spec`, the
  layered cache, monadic and purely functional builders (`buildMerkleTree`,
  `buildMerkleTree_with_hash`), proof generation and verification, and the
  bridging `simulateQ` lemmas that translate the monadic operations into their
  hash-function counterparts. Completeness of this construction is proven in
  `VCVio.CryptoFoundations.MerkleTree.Vector.Completeness`.

  ## Notes & TODOs

  We want this treatment to be as comprehensive as possible. In particular, our formalization
  should (eventually) include all complexities such as the following:

  - Multi-instance extraction & simulation
  - Dealing with arbitrary trees (may have arity > 2, or is not complete)
  - Path pruning optimization
-/

@[expose] public section

namespace MerkleTree

/- Lean 4.33 checks the subtype presentation of fixed-length lists at implicit transparency
when normalizing the dependent cache layers below. -/
attribute [local implicit_reducible] List.Vector

open List OracleSpec OracleComp

open scoped OracleSpec.PrimitiveQuery

variable (α : Type _)

/-- Define the domain & range of the (single) oracle needed for constructing a Merkle tree with
    elements from some type `α`.

  We may instantiate `α` with `BitVec n` or `Fin (2 ^ n)` to construct a Merkle tree for boolean
  vectors of length `n`. -/
@[reducible]
def spec : OracleSpec (α × α) := (α × α) →ₒ α

@[simp, grind =]
lemma domain_def : (spec α).Domain = (α × α) := rfl

@[simp]
lemma range_def {x} : (spec α).Range x = α := rfl

section

variable [DecidableEq α] [Inhabited α] [Fintype α]

/-- Example: a single hash computation -/
def singleHash {m : Type _ → Type _} [Monad m] [hq : HasQuery (spec α) m]
    (left : α) (right : α) : m α := do
  let out ← hq.query ⟨left, right⟩
  return out

/-- Cache for Merkle tree. Indexed by `j : Fin (n + 1)`, i.e. `j = 0, 1, ..., n`. -/
def Cache (n : ℕ) := (layer : Fin (n + 1)) → List.Vector α (2 ^ layer.val)

/-- Add a base layer to the cache -/
def Cache.cons (n : ℕ) (leaves : List.Vector α (2 ^ (n + 1))) (cache : Cache α n) :
    Cache α (n + 1) :=
  Fin.snoc cache leaves

/-- Removes the leaves layer to the cache, returning only the layers of the tree above this -/
def Cache.upper (n : ℕ) (cache : Cache α (n + 1)) :
    Cache α n :=
  Fin.init cache

/-- Returns the leaves of the cache -/
def Cache.leaves (n : ℕ) (cache : Cache α (n + 1)) :
    List.Vector α (2 ^ (n + 1)) := cache (Fin.last _)

/-- The unique cache layer of a depth-zero tree. -/
def Cache.base (leaves : List.Vector α 1) : Cache α 0 :=
  Fin.cases leaves (fun j => Fin.elim0 j)

omit [DecidableEq α] [Inhabited α] [Fintype α] in
@[simp]
lemma Cache.base_zero (leaves : List.Vector α 1) : Cache.base α leaves 0 = leaves := by
  unfold Cache.base
  exact Fin.cases_zero

omit [DecidableEq α] [Inhabited α] [Fintype α] in
@[simp, grind =]
lemma Cache.upper_cons (n : ℕ) (leaves : List.Vector α (2 ^ (n + 1))) (cache : Cache α n) :
    Cache.upper α n (Cache.cons α n leaves cache) = cache := by
  exact @Fin.init_snoc (n + 1)
    (fun i : Fin (n + 2) => List.Vector α (2 ^ i.val)) leaves cache

omit [DecidableEq α] [Inhabited α] [Fintype α] in
@[simp, grind =]
lemma Cache.leaves_cons (n : ℕ) (leaves : List.Vector α (2 ^ (n + 1))) (cache : Cache α n) :
    Cache.leaves α n (Cache.cons α n leaves cache) = leaves := by
  exact @Fin.snoc_last (n + 1)
    (fun i : Fin (n + 2) => List.Vector α (2 ^ i.val)) leaves cache

/-- Compute the next layer of the Merkle tree -/
def buildLayer {m : Type _ → Type _} [Monad m] [HasQuery (spec α) m]
    (n : ℕ) (leaves : List.Vector α (2 ^ (n + 1))) :
    m (List.Vector α (2 ^ n)) := do
  let leaves : List.Vector α (2 ^ n * 2) := cast (by rw [pow_succ]) leaves
  let pairs : List.Vector (α × α) (2 ^ n) :=
    List.Vector.ofFn (fun i =>
      (leaves.get ⟨2 * i, by omega⟩, leaves.get ⟨2 * i + 1, by omega⟩))
  let hashes : List.Vector α (2 ^ n) ←
    List.Vector.mmap (fun ⟨left, right⟩ => singleHash α left right) pairs
  return hashes

omit [DecidableEq α] [Inhabited α] [Fintype α] in
/-- Building the only internal layer of a two-leaf tree makes exactly their hash query. -/
@[simp]
lemma buildLayer_zero (a b : α) :
    buildLayer (m := OracleComp (spec α)) α 0 ⟨[a, b], rfl⟩ =
      (do
        let h ← ((spec α).query (a, b) : OracleComp (spec α) α)
        pure (⟨[h], rfl⟩ : List.Vector α (2 ^ 0))) := rfl

/-- Build the full Merkle tree, returning the cache -/
def buildMerkleTree (α) {m : Type _ → Type _} [Monad m] [HasQuery (spec α) m]
    (n : ℕ) (leaves : List.Vector α (2 ^ n)) :
    m (Cache α n) :=
  match n with
  | 0 => do
    return Cache.base α leaves
  | n + 1 => do
    let lastLayer ← buildLayer α n leaves
    let cache ← buildMerkleTree α n lastLayer
    return Cache.cons α n leaves cache

/-- Get the root of the Merkle tree -/
@[simp, grind]
def getRoot {n : ℕ} (cache : Cache α n) : α :=
  (cache 0).get ⟨0, by simp⟩

omit [DecidableEq α] [Inhabited α] [Fintype α] in
/-- Adding a leaf layer to a cache does not change its root. -/
@[simp high]
lemma getRoot_cons (n : ℕ) (leaves : List.Vector α (2 ^ (n + 1))) (cache : Cache α n) :
    getRoot α (Cache.cons α n leaves cache) = getRoot α cache := by
  change getRoot α (Cache.upper α n (Cache.cons α n leaves cache)) = getRoot α cache
  rw [Cache.upper_cons]

/-- Figure out the indices of the Merkle tree nodes that are needed to
recompute the root from the given leaf -/
def findNeighbors {n : ℕ} (i : Fin (2 ^ n)) (layer : Fin n) :
    Fin (2 ^ (layer.val + 1)) :=
  -- `finFunctionFinEquiv.invFun` gives the little-endian order, e.g. `6 = 011 little`
  -- so we need to reverse it to get the big-endian order, e.g. `6 = 110 big`
  let bits := (Vector.ofFn (finFunctionFinEquiv.invFun i)).reverse
  -- `6 = 110 big`, `layer = 1`, we get `neighbor = 10 big`
  let m := layer.val + 1
  have hm : m ≤ n := Nat.succ_le_of_lt layer.isLt
  let neighbor : _root_.Vector (Fin 2) m :=
    Vector.ofFn fun j : Fin m =>
      let j' : Fin n := ⟨j.val, lt_of_lt_of_le j.isLt hm⟩
      let bit := bits.get j'
      if j.val = layer.val then bit + 1 else bit
  -- `10 big` => `01 little` => `2`
  finFunctionFinEquiv.toFun neighbor.reverse.get

end

@[simp]
theorem getRoot_base (leaves : List.Vector α 1) :
    getRoot α (Cache.base α leaves) = leaves.head := by
  simp [getRoot, List.Vector.head]

@[simp, grind =]
theorem getRoot_zero {m : Type _ → Type _} [Monad m] [LawfulMonad m]
    [HasQuery (spec α) m] (leaves : List.Vector α 1) :
    getRoot α <$> buildMerkleTree (m := m) α 0 leaves = pure leaves.head := by
  let cache : Cache α 0 := Cache.base α leaves
  change getRoot α <$> (pure cache : m (Cache α 0)) = pure leaves.head
  rw [map_pure]
  simp [cache]

@[simp, grind =]
theorem getRoot_trivial {m : Type _ → Type _} [Monad m] [LawfulMonad m]
    [HasQuery (spec α) m] (a : α) :
    getRoot α <$> (buildMerkleTree (m := m) α 0 ⟨[a], rfl⟩) = pure a := by
  calc
    _ = pure (List.Vector.head (⟨[a], rfl⟩ : List.Vector α 1)) :=
      getRoot_zero (m := m) α ⟨[a], rfl⟩
    _ = pure a := rfl

@[simp, grind =]
theorem getRoot_single (a b : α) :
    getRoot α <$> buildMerkleTree (m := OracleComp (spec α)) α 1 ⟨[a, b], rfl⟩ =
      ((spec α).query (a, b)) := by
  simp only [buildMerkleTree, buildLayer_zero, map_bind, map_pure, pure_bind, bind_assoc]
  change ((spec α).query (a, b) : OracleComp (spec α) α) >>= pure = _
  rw [bind_pure]

section

variable [DecidableEq α] [Inhabited α] [Fintype α]

/-- Sibling index in a perfect binary tree layer indexed by `Fin (2 ^ (n + 1))`. -/
def siblingIndex {n : ℕ} (i : Fin (2 ^ (n + 1))) : Fin (2 ^ (n + 1)) :=
  if h : i.val % 2 = 0 then
    ⟨i.val + 1, by omega⟩
  else
    ⟨i.val - 1, by omega⟩

/-- Generate a Merkle proof that a given leaf at index `i` is in the Merkle tree. The proof consists
  of the Merkle tree nodes that are needed to recompute the root from the given leaf. -/
def generateProof {n : ℕ} (i : Fin (2 ^ n)) (cache : Cache α n) :
    List.Vector α n :=
  match n with
  | 0 => List.Vector.nil
  | n + 1 =>
      List.Vector.cons ((cache.leaves).get (siblingIndex i))
        (generateProof ⟨i.val / 2, by omega⟩ (cache.upper))

/--
Given a leaf index, a leaf at that index, and putative proof,
returns the hash that would be the root of the tree if the proof was valid.
i.e. the hash obtained by combining the leaf in sequence with each member of the proof,
according to its index.
-/
def getPutativeRoot {m : Type _ → Type _} [Monad m] [HasQuery (spec α) m]
    {n : ℕ} (i : Fin (2 ^ n)) (leaf : α) (proof : List.Vector α n) :
    m α :=
  match n with
  | 0 => do
    return leaf
  | n + 1 => do
    let signBit := i.val % 2
    let i' : Fin (2 ^ n) := ⟨i.val / 2, by omega⟩
    if signBit = 0 then
      let newLeaf ← singleHash α leaf proof.head
      getPutativeRoot i' newLeaf proof.tail
    else
      let newLeaf ← singleHash α proof.head leaf
      getPutativeRoot i' newLeaf proof.tail

/-- Verify a Merkle proof `proof` that a given `leaf` at index `i` is in the Merkle tree with given
  `root`.
  Works by computing the putative root based on the branch, and comparing that to the actual root.
  Returns `true` if the proof is valid, and `false` otherwise. -/
def verifyProof {m : Type _ → Type _} [Monad m] [HasQuery (spec α) m]
    {n : ℕ} (i : Fin (2 ^ n)) (leaf : α) (root : α) (proof : List.Vector α n) :
    m Bool := do
  let putative_root ← (getPutativeRoot α i leaf proof : m α)
  return (putative_root == root)

/-- A purely functional version of `buildLayer`, given an explicit hash function. -/
def buildLayer_with_hash (n : ℕ) (leaves : List.Vector α (2 ^ (n + 1))) (hashFn : α × α → α) :
    List.Vector α (2 ^ n) :=
  let leaves : List.Vector α (2 ^ n * 2) := cast (by rw [pow_succ]) leaves
  let pairs : List.Vector (α × α) (2 ^ n) :=
    List.Vector.ofFn (fun i =>
      (leaves.get ⟨2 * i, by omega⟩, leaves.get ⟨2 * i + 1, by omega⟩))
  pairs.map hashFn

/-- A purely functional version of `buildMerkleTree`, given an explicit hash function. -/
def buildMerkleTree_with_hash (n : ℕ) (leaves : List.Vector α (2 ^ n)) (hashFn : α × α → α) :
    Cache α n :=
  match n with
  | 0 => Cache.base α leaves
  | n + 1 =>
      let lastLayer := buildLayer_with_hash (α := α) n leaves hashFn
      let cache := buildMerkleTree_with_hash n lastLayer hashFn
      Cache.cons α n leaves cache

/--
A purely functional version of `getPutativeRoot`, given an explicit hash function.
-/
def getPutativeRoot_with_hash {n : ℕ} (i : Fin (2 ^ n)) (leaf : α) (proof : List.Vector α n)
    (hashFn : α × α → α) : α :=
  match n with
  | 0 => leaf
  | n + 1 =>
      let signBit := i.val % 2
      let i' : Fin (2 ^ n) := ⟨i.val / 2, by omega⟩
      if signBit = 0 then
        let newLeaf := hashFn (leaf, proof.head)
        getPutativeRoot_with_hash i' newLeaf proof.tail hashFn
      else
        let newLeaf := hashFn (proof.head, leaf)
        getPutativeRoot_with_hash i' newLeaf proof.tail hashFn

omit [DecidableEq α] [Inhabited α] [Fintype α] in
@[simp, grind =]
lemma simulateQ_singleHash (f : QueryImpl (spec α) Id) (left right : α) :
    simulateQ f (singleHash (m := OracleComp (spec α)) α left right) = f ⟨left, right⟩ := by
  simp [singleHash, simulateQ_query]

omit [DecidableEq α] [Inhabited α] [Fintype α] in
@[simp, grind =]
lemma simulateQ_listVector_mmap_singleHash (f : QueryImpl (spec α) Id) {m : ℕ}
    (xs : List.Vector (α × α) m) :
    simulateQ f (List.Vector.mmap
        (fun x => (singleHash α x.1 x.2 : OracleComp (spec α) α)) xs) =
      xs.map f := by
  induction xs using List.Vector.inductionOn with
  | nil => simp
  | @cons m x xs ih =>
    simp only [Vector.mmap_cons, Vector.map_cons, simulateQ_bind, simulateQ_singleHash, ih]
    rfl

omit [DecidableEq α] [Inhabited α] [Fintype α] in
@[simp, grind =]
lemma simulateQ_buildLayer_eq (f : QueryImpl (spec α) Id) (n : ℕ)
    (leaves : List.Vector α (2 ^ (n + 1))) :
    simulateQ f (buildLayer α n leaves) =
      buildLayer_with_hash (α := α) n leaves f := by
  simp [buildLayer, buildLayer_with_hash, simulateQ_listVector_mmap_singleHash]

omit [DecidableEq α] [Inhabited α] [Fintype α] in
@[simp, grind =]
lemma simulateQ_buildMerkleTree_eq (f : QueryImpl (spec α) Id) (n : ℕ)
    (leaves : List.Vector α (2 ^ n)) :
    simulateQ f (buildMerkleTree α n leaves) =
      buildMerkleTree_with_hash (α := α) n leaves f := by
  induction n with
  | zero => rfl
  | succ n ih =>
    simp only [buildMerkleTree, buildMerkleTree_with_hash, simulateQ_bind,
      simulateQ_buildLayer_eq, ih]
    rfl

omit [DecidableEq α] [Inhabited α] [Fintype α] in
@[simp, grind =]
lemma simulateQ_getPutativeRoot_eq (f : QueryImpl (spec α) Id) {n : ℕ} (i : Fin (2 ^ n))
    (leaf : α) (proof : List.Vector α n) :
    simulateQ f (getPutativeRoot α i leaf proof) =
      getPutativeRoot_with_hash (α := α) i leaf proof f := by
  induction n generalizing leaf with
  | zero => rfl
  | succ n ih =>
    by_cases hsign : i.val % 2 = 0 <;>
      · simp [getPutativeRoot, getPutativeRoot_with_hash, hsign, ih]
        rfl

end

end MerkleTree
